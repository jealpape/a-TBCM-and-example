! This function was taken from Ingo Titze's 'posture.for' for speak.
! Convertion to APDL on 2/28/01 by eric hunter
!
! This function represents Equation 18,19, & 20 from the paper...
! E. J. Hunter, I. R. Titze, and F. Alipour, (2004) A three-dimensional 
! model of vocal fold adduction/abduction. J.Acoust.Soc.Am. 115 (4):1747-1759
!
! USAGE:
! a  ,eps ,sigm,bb  ,epm ,depm,deps
!arg1,arg2,arg3,arg4,arg5,arg6,arg7

!!!!!!!! Ingo Titze's comments from original Fortran Code
!  Function 'active' computes the active stress-strain curve for
!  vocal fold tissues. There is a strain-dependent factor and a
!  strain-rate-dependent factor.
!	function active(a,eps,sigm,b,epm,depm,deps)
!!!!!!!!!!!!!!!!!!!!!!!!!!

!/nopr	! no output to output window

x_tmp_=arg7/arg6				!strain rate ratio
*if,arg7,le,0,then			!tissue is shortening
	!! Maximum returns maximum of two variables in tmp_
	maximum,0,((x_tmp_+1)/(1-3*x_tmp_))
	g_tmp_	=tmp_
*else					!tissue is lengthening
	g_tmp_=(x_tmp_+1/9)/(5*x_tmp_/9+1/9)
*endif
maximum,0,(1-arg4*(arg2-arg5)**2)	!get max of these and put in tmp_
f_tmp_=tmp_					!max. isometric stess
tmp_=arg1*arg3*f_tmp_*g_tmp_			!active stress

! Clean up all variables
! $ allows multiple commands on one line
x_tmp_= $ g_tmp_= $ max_= $ f_tmp_=

!/gopr	! resume output to output window


!!!!!!! original Fortran code
!x_=deps_/depm_					!strain rate ratio
!*if(deps_.le.0) then				!tissue is shortening
!	g_=max(0,((x_+1)/(1-3*x_)))
!*else						!tissue is lengthening
!	g_=(x_+1/9)/(5*x_/9+1/9)
!*endif
!maximum,0,(1-b_*(eps_-epm_)**2)	!get max of these and put in max_
!f_=max_				!max. isometric stess
!siga_=a*sigm_*f_*g_					!active stress
